    model {
    # Model 1f: Aboveground biomass as function of date, otter index and sediment type, 
	# 	quadratic day effect and linear otter effect 
	# 
	# Set Priors: 
	#
    # Fixed effect parameters 
	beta0 ~ dnorm(0,.001)	# intercept of linear model
	beta1 ~ dnorm(0,.001)	# beta1 = effect of julian date
	beta1b ~ dnorm(0,.001)	# beta1b = quadratic effect of date
	beta2 ~ dnorm(0,.001)	# beta2 = effect of otter index	
	# beta3 ~ dnorm(0,.001)	# beta3 = effect of sediment type
	# 
    # Priors for variance params: 
    sigS ~ dunif(0.001,10) # Variance among sites
    tauS <- pow(sigS,-2)
    sigO ~ dunif(0.001,10) # Variance among log(Obs) within a site
    tauO <- pow(sigO,-2)
	# Dispers ~ dgamma(1.1,0.1)   # over-dispersion parameter (for Neg Binom count variable)
	#
    # Compute random effects for sites 
    for (s in 1:Nsites){
		eps[s] ~ dnorm(0,tauS)
	}
	# Cycle through observations to estimate observed node
	for (i in 1:Nobs){
		# Expected response variable, mu (for lognormal, mu is log of median Obs)
		Mu[i] <- beta0 + beta1*Day[i] + beta1b*(Day[i]^2) + beta2*(Ott[i]) + eps[Site[i]] # + beta2b*(Ott[i]^2) 
		# Observed node as random draw from appropriate probability distribution 
		Obs[i] ~ dlnorm(Mu[i],tauO) # observed var drawn from log-normal distribution
		# Compute negative log likelihood for this observation
		# (use appropriate log-density distribution, e.g. norm, lnorm, negbin, etc.) 
		loglik[i] <- logdensity.lnorm(Obs[i],Mu[i],tauO)
	}
  }
